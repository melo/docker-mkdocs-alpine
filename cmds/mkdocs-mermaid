#!/usr/bin/env perl
#
# A filter that converts mermaidJS fenced blocks to HTML. Scans all folders
# given as parameter for markdown files and edits them in-place.
#
# This replaces stuff like:
#
#      ```mermaid
#      ...
#      ```
#
# With:
#
#      <pre><code class="mermaid">
#      ...
#      </code></pre>
#
# And if a mermaid block is found, it will add the MermaidJS lib at the bottom,
# and start it up.
#
# The filter is a two part:
#
#  * the first run is over the markdown files to convert the fenced blocks
#    to HTML;
#  * the second is should be run over the HTML files to make sure we load the
#    required JS files.
#

use 5.022;
use warnings;
use Path::Tiny;
use Getopt::Long;

sub usage {
  print "FATAL: @_\n" if @_;
  die <<EOU;
Usage: mkdocs-mermaid [<options>] <dir>...

Parses <dir> for Markdown and/or HTML files to support MermaidJS
fenced blocks in the final site.

Requires two passes, the first one with --markdown to deal with the
fenced blocks, and the second with --html to load the required
JS libs.

EOU
}

GetOptions(\my %opts, 'help|?', 'markdown', 'html') or usage();
usage() if $opts{hellp};
usage('One of --markdown or --html is required') unless $opts{markdown} or $opts{html};

convert_folder($_) for @ARGV;


#### Utilities

sub convert_folder {
  my $folder = path(shift);
  die "ERROR: path '$folder' is not a folder\n" unless $folder->is_dir;

  my $iter = $folder->iterator({ recurse => 1 });
  while (my $f = $iter->()) {
    if ($opts{markdown}) {
      next unless $f->is_file and $f->basename =~ m/\.md$/;
      $f->edit(\&replace_mermaid_fenced_block_with_html);
    }
    if ($opts{html}) {
      next unless $f->is_file and $f->basename =~ m/\.html$/;
      $f->edit(\&enable_mermaid_js);
    }
  }
}

sub replace_mermaid_fenced_block_with_html {
  return unless m/^ ``` mermaid/gsmx;

  ## Awesome, I love perl regexp's... ;)
  s{^ ``` mermaid $ (.+?) ^ ```}{<pre><code class="mermaid">$1</code></pre>}gsmx;

  return $_;
}

sub enable_mermaid_js {
  return unless m/<code class="mermaid">/gsm;

  ## Enable the JS lib on this document
  s{</body>}{<script src="https://unpkg.com/mermaid\@8.5.2/dist/mermaid.min.js"></script><script>mermaid.initialize({startOnLoad:true});</script></body>}gsm;
}